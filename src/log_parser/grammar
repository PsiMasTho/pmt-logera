%token-path ../log_scanner/log_token_enums.h

%scanner ../log_scanner/log_scanner.h
%scanner-class-name LogScanner

%token IDENT
%token ATTR
%token DATE
%token VALUE

%stype std::string

%%

input:
    // empty
|
    input line
;

line:
    date '\n'
    {
       d_logDataModifier.setDate(Date($1));
    }
|
    assign_attributes
|
    '\n'
;

date:
    DATE
    {
        $$ = d_scanner.matched();
    }
;

value:
    VALUE
    {
        $$ = std::string(d_scanner.matched()).substr(1); // skip '$' char
    }
;

assign_attributes:
    identifier ':' '\n' attribute value
    {
        d_logDataModifier.startNewLineForNewVar($1);
        d_logDataModifier.addAttrToCurrentLine($4, $5);
    }
|
    assign_attributes attribute value
    {
        d_logDataModifier.addAttrToCurrentLine($2, $3);
    }
|
    '\n' attribute value
    {
        d_logDataModifier.startNewLineForOldVar();
        d_logDataModifier.addAttrToCurrentLine($2, $3);
    }
;

identifier:
    IDENT
    {
        $$ = d_scanner.matched();
    }
;

attribute:
    ATTR
    {
        $$ = std::string(d_scanner.matched()).substr(1);
    }
;